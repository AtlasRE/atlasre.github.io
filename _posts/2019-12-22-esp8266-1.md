---
layout: post
title: ESP8266 (一)
date: 2019-12-22 22:44:03
tags: [arduino, ESP8266]
---
### 1 用arduino ide给ATK-ESP8266烧录程序
ESP8266团队为arduino ide开发了一套[支持库](https://github.com/esp8266/Arduino)，允许用户以类似arduino编程的方式为ESP8266进行编程，而不使用AT指令。配置方法如下：
1. 配置arduino ide<br>
   首先需要在ide中添加ESP8266设备的支持。在文件 -> 首选项 -> 附加开发板管理器一栏中，填入`http://arduino.esp8266.com/stable/package_esp8266com_index.json`。打开工具 -> 开发板 -> 开发板管理器，ide会自动更新支持设备的列表，搜索ESP8266：
   ![ESP8266库](https://s2.ax1x.com/2019/12/18/Q74ZA1.png)
   安装完了就可以在开发板中找到ESP8266相关设备了。选Generic ESP8266 Module即可。
2. ATK-ESP8266连接电脑<br>
   找个USB-TTL转接器连上WIFI模块。ESP8266上的TXD连TX，RXD连RX，VCC接3.3V。如果是烧录程序，IO_0需要**接地**，否则不用接在任何地方。IO_0默认高电平为运行程序模式。<br>
   接好后连上电脑，需要先安装CH340驱动。Windows在[这里](http://www.wch.cn/downloads/CH341SER_EXE.html)下载，Linux版make可能会出现错误，解决方法参考[这篇文章](https://blog.csdn.net/nuttiny/article/details/79842900)。
3. 为ESP8266编程须先引入`ESP8266WiFi.h`。一些其他功能例如HTTP与UDP需要引入其他头文件。具体方法参考：
   - [ESP8266库官方文档](https://arduino-esp8266.readthedocs.io/en/2.6.3/index.html)（2.6.3版本）
   - [一个很不错的教程](https://blog.csdn.net/dpjcn1990/article/details/92829821)
4. ESP8266串口默认波特率为115200，如果想用串口监视器查看需要调至对应波特率。波特率可以通过`Serial.begin(baudRate)`进行修改。未知波特率可以通过`Serial.detectBaudrate()`进行查询。
5. 连接正确后就可以为ESP8266烧录程序了。烧录时一定要将IO_0接地，否则会无法识别模块。

### 2 ESP8266与python简单通信
设置ESP8266为STA模式，与python通信

python端
1. 建立socket对象，绑定服务器，监听端口；
2. 接收数据，进行模型预测，返回预测结果
```python
import socket
import random
import numpy as np

def parse_data_string(data_string):
    data = data_string.split('|')
    data = [float(num) for num in data]
    return data

def model(sensor_data):
    # 模型预测的部分，随便写的
    # 结果为0或1
    r = random.random()
    res = int(np.around(r))
    return res

host = "192.168.20.223"
port = 8080

server = socket.socket(socket.AF_INET, socket.SOCK_STREAM)
server.bind((host, port))
server.listen(0)

print("listening to port: " + str(host) + ":" + str(port))
connection, address = server.accept()

while True:
    # 当接收到数据时，在服务器端对数据进行判断并返回结果
    data_string = connection.recv(1024)
    sensor_data = parse_data_string(data_string.decode('utf-8'))

    res = model(sensor_data)
    connection.send(str(res).encode('utf-8'))
```
ESP8266
1. 设置ESP8266为STA模式，连接Wifi
2. 连接服务器
3. 发送传感器数据
4. 接收返回的模型预测结果
```c++
#include <ESP8266WiFi.h>
//#include <ESP8266HTTPClient.h>

const char *AP_SSID = "Future Lab-6F";
const char *AP_PSK = "weilaishiyanshi";

const uint16_t port = 8080;
const char *host = "192.168.20.223";

WiFiClient client;

const int N_SENSOR = 4;

// 传感器信息，这里随便写了几个数
float sensor_data[N_SENSOR] = {1.26, 3.47, 2.59, 7.63};

int connectToServer(const char *, const uint16_t);
String parseSensorData(float *);

void setup()
{
  Serial.begin(115200);
  Serial.println();

  WiFi.begin(AP_SSID, AP_PSK);

  Serial.print("Connecting");
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(".");
  }
  Serial.println();

  Serial.print("Connected, IP address: ");
  Serial.println(WiFi.localIP());

  connectToServer(host, port);
//  client.println("hey there!");
}

void loop()
{
  if (!client.connected())
  {
    Serial.println("Connection lost. Reconnect in 5 seconds...");
    delay(5000);
    connectToServer(host, port);
  }
  
  /*
   * manipulate and construct sensor data array here
   */

  String send_data = parseSensorData(sensor_data);
  client.print(send_data);
  
  while (!client.available());
  String recv = client.readStringUntil('\n');
  Serial.println(recv);
}

int connectToServer(const char *host, const uint16_t port)
{
  // Connecting to server using WiFiClient
  Serial.print("Connecting to server: ");
  Serial.println(host);
  int i = 0;
  while (!client.connect(host, port) && i < 20)
  {
    i++;
    delay(500);
    Serial.print(".");
  }
  if (i == 20)
    return 0;
  else
  {
    Serial.print("ESP8266 Module connected to: ");
    Serial.printf("%s:%d\n", host, port);
    return 1;
  }
}

String parseSensorData(float *data)
{
  String data_string = "";
  for (int i = 0; i < N_SENSOR; i++)
  {
    data_string.concat(String(data[i]));
    if (i != N_SENSOR - 1)
      data_string.concat('|');
  }

  return data_string;
}
```
程序运行效果：
![效果](https://s2.ax1x.com/2019/12/18/Q74Ib9.png)
左侧程序显示由ESP8266发送至服务器的数据。右侧为服务器返回的模型预测结果。

### 3 ESP8266与arduino通过软串口进行通信
通过`SoftwareSerial`库使ESP8266与arduino之间进行通信，传输传感器数据与模型预测结果。如果结果为1则arduino上led闪烁报警。<br>
如果用硬串口进行通信，则ESP8266上TXD接TX，RXD接RX，**软串口则需要反接，模块的TXD接arduino的RX，RXD接arduino的TX。**
1. arduino发送传感器数据至ESP8266
2. ESP8266对数据进行封装，发送并接收模型预测结果
3. ESP8266通过串口返回结果
4. arduino根据结果作出响应
```c++
#include <SoftwareSerial.h>

// 指定软串口的RX, TX引脚
SoftwareSerial mySerial(10, 11);
int led = 13;

void setup()
{
  Serial.begin(115200);
  mySerial.begin(115200);
  pinMode(led, OUTPUT);
  delay(1000);
  Serial.println("go");
}

void loop()
{
  if (mySerial.available())
  {
    char res = mySerial.read();
    Serial.println(res);
    if (res == '1')
    {
      for (int i = 0; i < 5; i++)
      {
        digitalWrite(led, HIGH);
        delay(500);
        digitalWrite(led, LOW);
        delay(500);
      }
    }
  }
}
```